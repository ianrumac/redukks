apply plugin: 'maven-publish'
apply plugin: 'signing'

// Stub secrets to let the project sync and build without the publication values set up
project.ext['signing.keyId'] = null
project.ext['signing.password'] = null
project.ext['signing.key'] = null
project.ext['ossrhUsername'] = null
project.ext['ossrhPassword'] = null

// Grabbing secrets from local.properties file or from environment variables, which could be used on CI
def secretPropsFile = project.rootProject.file("local.properties")
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
    p.each { name, value -> project.ext[name] = value }
} else {
    project.ext['signing.keyId'] = System.getenv("SIGNING_KEY_ID")
    project.ext['signing.password'] = System.getenv("SIGNING_PASSWORD")
    project.ext['signing.key'] = System.getenv("SIGNING_KEY")
    project.ext['ossrhUsername'] = System.getenv("OSSRH_USERNAME")
    project.ext['ossrhPassword'] = System.getenv("OSSRH_PASSWORD")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
}

String getExtraString(String name) {
    return project.ext[name]?.toString()
}

publishing {
    // Configure maven central repository
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = getExtraString("ossrhUsername")
                password = getExtraString("ossrhPassword")
            }
        }
    }

    // Configure all publications
    publications.withType(MavenPublication) {

        // Stub javadoc.jar artifact
        artifact tasks.javadocJar
        version PUBLISH_VERSION

        // Provide artifacts information requited by Maven Central
        pom {
            name = "Redukks Multiplatform"
            description = "Redukks library for Redux-like state management in Kotlin Multiplatform"
            url = "https://github.com/ianrumac/redukks"
            licenses {
                license {
                    name = "MIT"
                    url = "https://opensource.org/licenses/MIT"
                }
            }
            developers {
                developer {
                    id = "ianrumac"
                    name = "Ian Rumac"
                    email = "ianrumac@gmail.com"
                }
            }
            scm {
                url = "https://github.com/ianrumac/redukks"
            }
        }
    }
}

// Signing artifacts. Signing.* extra properties values will be used
signing {
    def keyId = project.ext.get('signing.keyId')
    def secretKeyBase64 = project.ext.get('signing.key')
    def secretKey = base64Decode(secretKeyBase64)
    def password = project.ext.get('signing.password')
    useInMemoryPgpKeys(keyId, secretKey, password)
    sign publishing.publications
}


def base64Decode(encodedString){
    if(encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    }
    return null;
}